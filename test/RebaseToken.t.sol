// SPDX-License-Identifier: MIT
pragma solidity 0.8.30;

import {Test, console2} from "forge-std/Test.sol";
import {RebaseToken} from "../src/RebaseToken.sol";
import {Vault} from "../src/Vault.sol";
import {IRebaseToken} from "../src/interfaces/IRebaseToken.sol";

contract RebaseTokenTest is Test {
    RebaseToken public rebaseToken;
    Vault public vault;

    address public owner = makeAddr("owner");
    address public user = makeAddr("user");

    function setUp() public {
        vm.startPrank(owner);
        // Deploy RebaseToken with owner as initial mint/burn recipient
        rebaseToken = new RebaseToken(owner);
        // Deploy Vault
        vault = new Vault(IRebaseToken(address(rebaseToken)));
        
        // Grant MINT_AND_BURN_ROLE to Vault
        rebaseToken.grantRole(rebaseToken.MINT_AND_BURN_ROLE(), address(vault));
        
        // Label addresses for better debugging
        vm.label(owner, "Owner");
        vm.label(user, "User");
        vm.label(address(rebaseToken), "RebaseToken");
        vm.label(address(vault), "Vault");
        
        vm.stopPrank();
    }

    function testSetUpCorrectly() public view {
        assertEq(address(vault.getRebaseTokenAddress()), address(rebaseToken));
        assertTrue(rebaseToken.hasRole(rebaseToken.MINT_AND_BURN_ROLE(), address(vault)));
    }

    function testDepositAndRedeem() public {
        uint256 depositAmount = 10 ether;
        vm.deal(user, depositAmount);

        vm.startPrank(user);
        
        // 1. Deposit
        vault.deposit{value: depositAmount}();
        
        // Check balances
        assertEq(rebaseToken.balanceOf(user), depositAmount);
        assertEq(address(vault).balance, depositAmount);

        // 2. Redeem half
        uint256 redeemAmount = 5 ether;
        vault.redeem(redeemAmount);

        // Check balances
        assertEq(rebaseToken.balanceOf(user), depositAmount - redeemAmount);
        assertEq(address(vault).balance, depositAmount - redeemAmount);
        assertEq(user.balance, redeemAmount);
        
        vm.stopPrank();
    }

    function testRedeemMax() public {
        uint256 depositAmount = 10 ether;
        vm.deal(user, depositAmount);

        vm.startPrank(user);
        
        // Deposit
        vault.deposit{value: depositAmount}();
        
        // Redeem Max
        vault.redeem(type(uint256).max);

        // Check balances (should be 0 tokens, full ETH back)
        assertEq(rebaseToken.balanceOf(user), 0);
        assertEq(user.balance, depositAmount);
        
        vm.stopPrank();
    }

     function testDepositLinear(uint256 amount) public {
        // Deposit funds
        amount = bound(amount, 1e5, type(uint96).max);
        // why are we using bound instead of assume ? 
        // bound is used to limit the range of values that can be generated by the fuzzing engine
        // assume is used to filter out values that do not meet a certain condition

        // 1. deposit
        vm.startPrank(user);
        vm.deal(user, amount);
        vault.deposit{value: amount}();

        // 2. check our rebase token balance
        uint256 startBalance = rebaseToken.balanceOf(user);
        console2.log("block.timestamp", block.timestamp);
        console2.log("startBalance", startBalance);
        assertEq(startBalance, amount);

        // 3. warp the time and check the balance again
        vm.warp(block.timestamp + 1 hours);
        console2.log("block.timestamp", block.timestamp);
        uint256 middleBalance = rebaseToken.balanceOf(user);
        console2.log("middleBalance", middleBalance);
        assertGt(middleBalance, startBalance);

        // 4. warp the time again by the same amount and check the balance again
        vm.warp(block.timestamp + 1 hours);
        uint256 endBalance = rebaseToken.balanceOf(user);
        console2.log("block.timestamp", block.timestamp);
        console2.log("endBalance", endBalance);
        assertGt(endBalance, middleBalance);

        assertApproxEqAbs(endBalance - middleBalance, middleBalance - startBalance, 1);
        // why not using assertEq ?
        // assertEq is used to check if two values are equal, it is not used here because the balanceOf is returning after divided by PRECESION_FACTOR  
        // assertApproxEqAbs is used to check if two values are approximately equal

        vm.stopPrank();
    }

    function testRedeemWithInterest() public {
        uint256 depositAmount = 10 ether;
        vm.deal(user, depositAmount);
        
        // 1. User deposits
        vm.startPrank(user);
        vault.deposit{value: depositAmount}();
        vm.stopPrank();

        // 2. Warp time (1 year) to accrue interest
        vm.warp(block.timestamp + 150 days);
        
        // 3. Check balance has grown
        uint256 balanceWithInterest = rebaseToken.balanceOf(user);
        assertGt(balanceWithInterest, depositAmount);

        // 4. Fund the Vault
        // The vault only has the original 10 ETH. It needs more ETH to pay the interest.
        vm.deal(address(vault), 100 ether);
        console2.log("address(vault).balance", address(vault).balance);

        // 5. Redeem everything
        vm.startPrank(user);
        vault.redeem(balanceWithInterest);
        vm.stopPrank(); 
        console2.log("user.balance", user.balance);

        // 6. Verify
        assertEq(user.balance, balanceWithInterest);    
        assertEq(rebaseToken.balanceOf(user), 0);
    }

    function testCannotRedeemWhenPaused() public {
        uint256 depositAmount = 10 ether;
        vm.deal(user, depositAmount);
        
        // Deposit first
        vm.startPrank(user);
        vault.deposit{value: depositAmount}();
        vm.stopPrank();

        // Pause the contract
        vm.prank(owner);
        vault.pause();

        // Try to redeem
        vm.startPrank(user);
        vm.expectRevert(); // Should revert with EnforcedPause()
        vault.redeem(depositAmount);
        vm.stopPrank();
    }
}